PROGRAM EVP_CONVERG
!=======================================================================
! [USAGE]: 
! CHECK CONVERGENCE OF EIGENVALUE
!
! [UPDATES]:
! LAST UPDATE ON DEC 3, 2020
!=======================================================================
USE omp_lib
USE MPI
USE MOD_MISC
USE MOD_BANDMAT
USE MOD_EIG
USE MOD_FD
USE MOD_LIN_LEGENDRE
USE MOD_SCALAR3
USE MOD_FFT
USE MOD_LAYOUT
USE MOD_LEGOPS
USE MOD_MARCH
USE MOD_INIT
USE MOD_EVP

IMPLICIT NONE
INTEGER     :: N_mat1,N_mat2,II,JJ,KK
INTEGER     :: M_BAR,M_1,M_2,EIG_BAR_IND
REAL(P8)    :: K_BAR,K_VAL_1_0
COMPLEX(P8) :: EIG_BAR
REAL(P8),DIMENSION(:),ALLOCATABLE:: RUR0,RUP0,UZ0,ROR0,ROP0,OZ0
COMPLEX(P8),DIMENSION(:),ALLOCATABLE:: M_eig
COMPLEX(P8),DIMENSION(:,:),ALLOCATABLE:: M_mat, EIG_R_mat, EIG_L_mat
COMPLEX(P8),DIMENSION(:),ALLOCATABLE:: VECL_BAR,RUR_BAR,RUP_BAR,UZ_BAR,ROR_BAR,ROP_BAR,OZ_BAR

! MPI:
INTEGER:: MPI_GLB_PROCS, MPI_GLB_RANK, newcomm
! ADD PERTURB:
INTEGER:: IS,NDIM
REAL(P8):: rp1,ip1
! ZERO-CROSSING LIMIT:
INTEGER,PARAMETER:: CROSS_LIMIT = 4
! SCANNING:
CHARACTER(LEN=72):: FILENAME
INTEGER:: FID

! CALL MPI_INIT(IERR)
CALL MPI_INIT_THREAD(MPI_THREAD_SERIALIZED,MPI_THREAD_MODE,IERR)
IF (MPI_THREAD_MODE.LT.MPI_THREAD_SERIALIZED) THEN
    WRITE(*,*) 'The threading support is lesser than that demanded.'
    CALL MPI_ABORT(MPI_COMM_WORLD,1,IERR)
ENDIF


CALL MPI_COMM_SIZE(MPI_COMM_WORLD, MPI_GLB_PROCS, IERR)
CALL MPI_COMM_RANK(MPI_COMM_WORLD, MPI_GLB_RANK, IERR)
CALL MPI_Comm_split(MPI_COMM_WORLD, MPI_GLB_RANK, 0, newcomm, IERR) ! DIVIDE NR BY #OF MPI PROCS
CALL READCOM('NOECHO')
CALL READIN(5)

! {M_bar, AK_bar}: BASE FLOW
!=======================================================================
IF (MPI_GLB_RANK.EQ.0) THEN       
    FILENAME = 'almostdegen_read.input'
    CALL READIN(FILENAME,M_BAR,K_BAR,EIG_BAR_IND,M_1,K_VAL_1_0)
ENDIF

OPEN(12,FILE='./converg/nrchop.input',STATUS='OLD',ACTION='READ',IOSTAT=IS)
READ(12,*) NRCHOP
NR = NRCHOP+20
CLOSE(12)

CALL EIG_MATRIX(M_BAR,K_BAR,M_mat,M_eig,EIG_R_mat,EIG_L_mat,comm_grp=newcomm) ! calculate operator matrix and its corresponding eigenvalues

! Pick a target Sigma_bar
IF (MPI_GLB_RANK.EQ.0) THEN
    WRITE(*,*) 'NRCHOP: ', NRCHOP
    OPEN(11,FILE='./converg/eig_converg.output',STATUS='UNKNOWN',POSITION='APPEND',ACTION='WRITE',IOSTAT=IS)

    DO EIG_BAR_IND = 1,CROSS_LIMIT
        EIG_BAR = M_eig(EIG_BAR_IND)

        WRITE(*,*) 'EIG_BAR:', EIG_BAR
        ! WRITE(11,233) NRCHOP,',',M_BAR,',',K_BAR,',',EIG_BAR_IND,',',REAL(EIG_BAR),',',AIMAG(EIG_BAR)
        WRITE(11,233) NRCHOP,M_BAR,K_BAR,EIG_BAR_IND,REAL(EIG_BAR),AIMAG(EIG_BAR)
        233  FORMAT(I3,',',I2,',',F4.1,',',I2,',',2(G20.12,:,','))

        CALL EIG2VELVOR(M_BAR,K_BAR,EIG_BAR_IND,EIG_R_mat,EIG_L_mat,&
                        RUR_BAR,RUP_BAR,UZ_BAR,ROR_BAR,ROP_BAR,OZ_BAR,VECL_BAR,comm_grp=newcomm)
        call SAVE_VEL(RUR_BAR,RUP_BAR,UZ_BAR,'./converg/vel_MK_'//ITOA3(M_BAR)//'_'//ITOA4(INT(K_BAR*100))&
                       //'_IND_'//ITOA3(EIG_BAR_IND)//'_NRCHOP_'//ITOA3(NRCHOP)//'.output')
        
        DEALLOCATE(RUR_BAR,RUP_BAR,UZ_BAR,ROR_BAR,ROP_BAR,OZ_BAR,VECL_BAR)
    ENDDO

    CLOSE(11)
    DEALLOCATE(M_eig)
    DEALLOCATE(EIG_R_mat,EIG_L_mat)        
    WRITE(*,*) ''
ENDIF

! DEALLOCATE 
!======================================================================= 
CALL MPI_BARRIER(MPI_COMM_WORLD,IERR)
DEALLOCATE(RUR0, RUP0, UZ0, ROR0, ROP0, OZ0)
DEALLOCATE(M_mat)

CALL MPI_FINALIZE(IERR)

CONTAINS
!=======================================================================
!=================== PROGRAM-DEPENDENT SUBROUTINES =====================
!=======================================================================
SUBROUTINE EIG_MATRIX(MREAD,AKREAD,H,EIG_VAL,EIG_VEC_R,EIG_VEC_L,comm_grp)
    IMPLICIT NONE
    INTEGER,INTENT(IN)    :: MREAD, comm_grp
    REAL(P8),INTENT(INOUT)    :: AKREAD
    COMPLEX(P8),DIMENSION(:,:),ALLOCATABLE,INTENT(INOUT):: H
    COMPLEX(P8),DIMENSION(:),ALLOCATABLE,INTENT(INOUT),OPTIONAL:: EIG_VAL
    COMPLEX(P8),DIMENSION(:,:),ALLOCATABLE,INTENT(INOUT),OPTIONAL:: EIG_VEC_R, EIG_VEC_L

    INTEGER     :: NR_MK, I
    REAL(P8)    :: REI
    INTEGER     :: MPI_NR_SIZE,MPI_NR_INDEX
    ! REAL(P8),DIMENSION(:),ALLOCATABLE:: RUR0,RUP0,UZ0,ROR0,ROP0,OZ0
    COMPLEX(P8),DIMENSION(:),ALLOCATABLE:: RURU,RUPU,UZU,RORU,ROPU,OZU
    COMPLEX(P8),DIMENSION(:),ALLOCATABLE:: PSIU,CHIU,PSI1,CHI1,PSI2,CHI2,PSI3,CHI3

    ZLEN = 2*PI/AKREAD
    CALL MPI_BCAST(MREAD,1,MPI_INTEGER,0,MPI_COMM_WORLD,IERR)
    CALL MPI_BCAST(ZLEN,1,MPI_DOUBLE_PRECISION,0,MPI_COMM_WORLD,IERR)
    
    ! INIT 
    NTH = 2
    NX = 4
    NTCHOP = 2 ! M = 0, M = MREAD
    NXCHOP = 2 ! K = 0, K = AKREAD
    CALL LEGINIT(comm_grp,MREAD)
    IF (.NOT.ALLOCATED(RUR0)) THEN
        ALLOCATE(RUR0(NR),RUP0(NR),UZ0(NR),ROR0(NR),ROP0(NR),OZ0(NR))
        CALL INIT_LOOP(RUR0,RUP0,UZ0,ROR0,ROP0,OZ0) ! ALL IN PFF space
    ENDIF

    ! SPLIT NRCHOPDIM
    NR_MK = NRCHOPS(2)
    IF (ALLOCATED(H).AND.(SIZE(H,1).NE.NR_MK)) THEN
        DEALLOCATE(H)
        ALLOCATE(H(2*NR_MK,2*NR_MK))
    ELSEIF (.NOT.(ALLOCATED(H))) THEN
        ALLOCATE(H(2*NR_MK,2*NR_MK))
    ENDIF 
    H = CMPLX(0.D0,0.D0,P8)
    CALL DECOMPOSE(NR_MK, MPI_GLB_PROCS, MPI_GLB_RANK, MPI_NR_SIZE, MPI_NR_INDEX)

    ! MAIN JOB
    ALLOCATE(PSIU(NRCHOPDIM),CHIU(NRCHOPDIM))
    ALLOCATE(PSI1(NRCHOPDIM),CHI1(NRCHOPDIM))
    ALLOCATE(PSI2(NRCHOPDIM),CHI2(NRCHOPDIM))
    ALLOCATE(PSI3(NRCHOPDIM),CHI3(NRCHOPDIM))
    ALLOCATE(RURU(NRCHOPDIM),RUPU(NRCHOPDIM),UZU(NRCHOPDIM))
    ALLOCATE(RORU(NRCHOPDIM),ROPU(NRCHOPDIM),OZU(NRCHOPDIM))

    DO I = MPI_NR_INDEX+1,MPI_NR_INDEX+MPI_NR_SIZE
    ! ================================ PSI =================================
        PSIU = 0.D0
        CHIU = 0.D0
        PSIU(I) = 1.D0

        ! VISCOSITY/HYPERV
        CALL CHOPSET(2)
        IF (VISC%SW .EQ. 1) THEN
        CALL DEL2_MK(PSIU,PSI3)
        PSI3 = PSI3 * VISC%NU
        ELSE IF (VISC%SW .EQ. 2) THEN
        CALL CHOPSET(-2+VISC%P)
        CALL HELMP_MK(VISC%P,PSIU,PSI3,0.D0)
        IF (MOD(VISC%P/2,2).EQ.0) PSI3 = -PSI3
        PSI3 = PSI3 * VISC%NUP
        CALL CHOPSET( 2-VISC%P)
        ELSE
        PSI3 = 0.D0
        ENDIF
        CHI3 = 0.D0
        CALL CHOPSET(-2)

        CALL CHOPSET(3)
        ! NONLINEAR TERM: W0 X U'
        CALL PC2VEL_MK(PSIU,CHIU,RURU,RUPU,UZU)
        CALL RTRAN_MK(RURU,1)
        CALL RTRAN_MK(RUPU,1)
        CALL RTRAN_MK(UZU,1)
        CALL VPROD_MK(ROR0,ROP0,OZ0,RURU,RUPU,UZU)
        CALL PROJECT_MK(RURU,RUPU,UZU,PSI1,CHI1)

        ! NONLINEAR TERM: W' X U0
        CALL PC2VOR_MK(PSIU,CHIU,RORU,ROPU,OZU)
        CALL RTRAN_MK(RORU,1)
        CALL RTRAN_MK(ROPU,1)
        CALL RTRAN_MK(OZU,1)
        CALL VPROD_MK(RUR0,RUP0,UZ0,RORU,ROPU,OZU)
        CALL PROJECT_MK(RORU,ROPU,OZU,PSI2,CHI2)
        CALL CHOPSET(-3)
        ! IF (I.EQ.2) THEN
        !     CALL XXDX_MK(PSIU,RURU)
        !     CALL MCAT(ROP0)
        ! ENDIF

        PSI1 = -PSI1 + PSI2 + PSI3
        CHI1 = -CHI1 + CHI2 + CHI3
        PSI1(NRCHOPS(2)+1:) = 0.D0
        CHI1(NRCHOPS(2)+1:) = 0.D0

        H(:NR_MK,  I) = PSI1(:NR_MK)
        H(NR_MK+1:,I) = CHI1(:NR_MK)

    ! ================================ CHI =================================
        PSIU = 0.D0
        CHIU = 0.D0
        CHIU(I) = 1.D0

        CALL IDEL2_MK(CHIU,CHI1)
        CHIU = CHI1
        CHI1 = 0.D0

        IF (VISC%SW .NE. 0) THEN
        CHI3 = PSI3
        ELSE
        CHI3 = 0.D0
        ENDIF
        PSI3 = 0.D0

        CALL CHOPSET(3)
        ! NONLINEAR TERM: W0 X U'
        CALL PC2VEL_MK(PSIU,CHIU,RURU,RUPU,UZU)
        CALL RTRAN_MK(RURU,1)
        CALL RTRAN_MK(RUPU,1)
        CALL RTRAN_MK(UZU,1)
        CALL VPROD_MK(ROR0,ROP0,OZ0,RURU,RUPU,UZU)
        CALL PROJECT_MK(RURU,RUPU,UZU,PSI1,CHI1)

        ! NONLINEAR TERM: W' X U0
        CALL PC2VOR_MK(PSIU,CHIU,RORU,ROPU,OZU)
        CALL RTRAN_MK(RORU,1)
        CALL RTRAN_MK(ROPU,1)
        CALL RTRAN_MK(OZU,1)
        CALL VPROD_MK(RUR0,RUP0,UZ0,RORU,ROPU,OZU)
        CALL PROJECT_MK(RORU,ROPU,OZU,PSI2,CHI2)
        CALL CHOPSET(-3)

        PSI1 = -PSI1 + PSI2 + PSI3
        CHI1 = -CHI1 + CHI2 + CHI3
        PSI1(NRCHOPS(2)+1:) = 0.D0
        CHI1(NRCHOPS(2)+1:) = 0.D0

        H(:NR_MK,  I+NR_MK) = PSI1(:NR_MK)
        H(NR_MK+1:,I+NR_MK) = CHI1(:NR_MK)
    ENDDO
    ! DEALLOCATE( RUR0,RUP0,UZ0,ROR0,ROP0,OZ0 )
    DEALLOCATE( RUPU,UZU,RORU,ROPU,OZU )
    DEALLOCATE( PSIU,CHIU,PSI1,CHI1,PSI2,CHI2,PSI3,CHI3 )

    CALL MPI_ALLREDUCE(MPI_IN_PLACE,H,SIZE(H),MPI_DOUBLE_COMPLEX,MPI_SUM,&
    MPI_COMM_WORLD,IERR)

! OUTPUT:
    CALL MPI_BARRIER(MPI_COMM_WORLD,IERR)
    IF (MPI_GLB_RANK.EQ.0) THEN  ! START OF THE SERIAL PART
        !WRITE OUT THE 3D MATRIX IN THE SCALAR USING UNFORMATTED MODE
        
        IF (PRESENT(EIG_VAL)) THEN
            IF (ALLOCATED(EIG_VAL)) DEALLOCATE(EIG_VAL)
            ALLOCATE( EIG_VAL(2*NR_MK) )

            IF (PRESENT(EIG_VEC_L)) THEN
                IF (ALLOCATED(EIG_VEC_R)) DEALLOCATE(EIG_VEC_R) 
                IF (ALLOCATED(EIG_VEC_L)) DEALLOCATE(EIG_VEC_L) 
                ALLOCATE( EIG_VEC_R(2*NR_MK,2*NR_MK),EIG_VEC_L(2*NR_MK,2*NR_MK) )
                CALL EIGENDECOMPOSE(H,EIG_VAL,ER = EIG_VEC_R,EL = EIG_VEC_L)
            ELSEIF (PRESENT(EIG_VEC_R)) THEN
                IF (ALLOCATED(EIG_VEC_R)) DEALLOCATE(EIG_VEC_R) 
                ALLOCATE( EIG_VEC_R(2*NR_MK,2*NR_MK) )
                CALL EIGENDECOMPOSE(H,EIG_VAL,ER = EIG_VEC_R)
            ELSE
                CALL EIGENDECOMPOSE(H,EIG_VAL)
            ENDIF
        ENDIF

    ENDIF

    RETURN

END SUBROUTINE EIG_MATRIX
!=======================================================================
SUBROUTINE EIG2VELVOR(MREAD,AKREAD,EIG_IND,EIG_VEC_R,EIG_VEC_L,RUR,RUP,UZ,ROR,ROP,OZ,VEC_L,comm_grp)
    IMPLICIT NONE
    INTEGER,INTENT(IN)    :: MREAD, EIG_IND, comm_grp
    REAL(P8),INTENT(INOUT)    :: AKREAD
    COMPLEX(P8),DIMENSION(:,:),ALLOCATABLE,INTENT(INOUT):: EIG_VEC_R, EIG_VEC_L
    COMPLEX(P8),DIMENSION(:),ALLOCATABLE,INTENT(INOUT):: RUR,RUP,UZ,ROR,ROP,OZ,VEC_L

    INTEGER:: NR_MK
    COMPLEX(P8),DIMENSION(:),ALLOCATABLE:: PSI,CHI,CHI_TEMP
    REAL(P8):: ENE_MK

    REAL(P8):: ANG
    COMPLEX(P8):: PHASE_FACTOR

    IF ((M(2).NE.MREAD).OR.(ABS(ZLEN-2*PI/AKREAD).GE.1.0E-13)) THEN
        WRITE(*,*) 'EIG2VELVOR: SET NEW M AND AK'
        ZLEN = 2*PI/AKREAD
        NTH = 2
        NX = 4
        NTCHOP = 2 ! M = 0, M = MREAD
        NXCHOP = 2 ! K = 0, K = AKREAD
        CALL LEGINIT(comm_grp,MREAD)
    ENDIF

    NR_MK = SIZE(EIG_VEC_R,1)/2
    ALLOCATE(VEC_L(SIZE(EIG_VEC_R,1)))
    VEC_L(:) = EIG_VEC_L(:,EIG_IND)

    ALLOCATE(PSI(NRCHOPDIM),CHI(NRCHOPDIM),CHI_TEMP(NRCHOPDIM))  
    ALLOCATE(RUR(NRCHOPDIM),RUP(NRCHOPDIM),UZ(NRCHOPDIM))
    ALLOCATE(ROR(NRCHOPDIM),ROP(NRCHOPDIM),OZ(NRCHOPDIM))  

    PSI = 0.D0
    CHI = 0.D0
    PSI(:NR_MK) = EIG_VEC_R(:NR_MK,EIG_IND) !PSI
    CHI(:NR_MK) = EIG_VEC_R(NR_MK+1:,EIG_IND) !DEL2CHI
    CALL IDEL2_MK(CHI,CHI_TEMP)

    ! =========================== NORMALIZATION ============================
    ! 1). NORMALIZE THE ENERGY =============================================
    ! ENE_MK = ENERGY_MK(PSI,CHI_TEMP)

    ! PSI = PSI/ENE_MK**0.5
    ! CHI_TEMP = CHI_TEMP/ENE_MK**0.5
    ! VEC_L(:) = VEC_L(:)*ENE_MK**0.5
    ! CALL CHOPSET(3)
    ! ======================================================================

    ! 2). NORMALIZE THE MAX UP =============================================
    CALL CHOPSET(3)
    CALL PC2VEL_MK(PSI,CHI_TEMP,RUR,RUP,UZ)
    ! CALCULATE THE PHASE_FACTOR:
    CALL RTRAN_MK(RUP,1)
    ANG = ATAN2(IMAG(RUP(1)),REAL(RUP(1)))
    PHASE_FACTOR = EXP(-IU*ANG)
    RUP = RUP*PHASE_FACTOR ! RUP NOW PURELY REAL
    RUP = RUP/TFM%R ! RUP -> UP
    PHASE_FACTOR = PHASE_FACTOR/MAXVAL(ABS(RUP(:)))
    ! APPLY THE PHASE_FACTOR:
    PSI = PSI*PHASE_FACTOR
    CHI_TEMP = CHI_TEMP*PHASE_FACTOR
    VEC_L(:) = VEC_L(:)/PHASE_FACTOR
    RUR = 0.D0
    RUP = 0.D0
        UZ = 0.D0
    ! ======================================================================

    CALL PC2VEL_MK(PSI,CHI_TEMP,RUR,RUP,UZ)    
    CALL PC2VOR_MK(PSI,CHI_TEMP,ROR,ROP,OZ)
    CALL RTRAN_MK(RUR,1)
    CALL RTRAN_MK(RUP,1)
    CALL RTRAN_MK( UZ,1)
    CALL RTRAN_MK(ROR,1)
    CALL RTRAN_MK(ROP,1)
    CALL RTRAN_MK( OZ,1)
    CALL CHOPSET(-3)

    ! ========================= ADJUST EIG_VEC_R ===========================
    ! ================= ONLY CHANGE EIG_VEC_R(:,EIG_IND) ===================
    EIG_VEC_R(:NR_MK,EIG_IND) = PSI(:NR_MK) !PSI
    EIG_VEC_R(NR_MK+1:,EIG_IND) = CHI_TEMP(:NR_MK) !CHI
    ! WRITE(*,*) 'NORMED ENE:', ENERGY_MK(PSI,CHI_TEMP)      

    ! DEBUG:
    ! WRITE(*,*) 'MAX:'
    ! WRITE(*,*) MAXVAL(REAL(RUP/TFM%R))
    ! WRITE(*,*) MAXVAL(-REAL(RUP/TFM%R))

    DEALLOCATE(PSI,CHI,CHI_TEMP)

END SUBROUTINE EIG2VELVOR
! ======================================================================
SUBROUTINE SAVE_VEL(RUR,RUP,UZ,VEL_FILENAME)
    IMPLICIT NONE

    INTEGER:: VEL_FID=17
    CHARACTER(LEN=*):: VEL_FILENAME
    COMPLEX(P8),DIMENSION(:),ALLOCATABLE,INTENT(IN):: RUR,RUP,UZ

    open(VEL_FID,FILE=VEL_FILENAME,STATUS='unknown',ACTION='WRITE',IOSTAT=IS)

    DO II = 1,NR
        WRITE(VEL_FID,117) TFM%R(II),REAL(RUR(II))/TFM%R(II),AIMAG(RUR(II))/TFM%R(II) &
                                    ,REAL(RUP(II))/TFM%R(II),AIMAG(RUP(II))/TFM%R(II) &
                                    ,REAL(UZ(II)),AIMAG(UZ(II))
    ENDDO
    
117  FORMAT(7(G20.12,:,','))
    close(VEL_FID)

END SUBROUTINE SAVE_VEL

END PROGRAM EVP_CONVERG
!=======================================================================
